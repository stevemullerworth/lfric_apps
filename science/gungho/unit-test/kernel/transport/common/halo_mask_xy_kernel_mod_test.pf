!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

module halo_mask_xy_kernel_mod_test

  use funit
  use constants_mod,          only : i_def

  implicit none

  private
  public :: halo_mask_xy_test_type , test_all

  @TestCase
  type, extends(TestCase) :: halo_mask_xy_test_type
    private
  contains
    procedure test_all
  end type halo_mask_xy_test_type


contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use halo_mask_xy_kernel_mod,       only: halo_mask_xy_code
    use get_unit_test_m3x3_dofmap_mod, only: get_m3x3_stencil_dofmap_cross, &
                                             get_w3_m3x3_dofmap

    implicit none

    class(halo_mask_xy_test_type), intent(inout) :: this

    integer(kind=i_def)              :: nlayers
    integer(kind=i_def)              :: cell
    integer(kind=i_def)              :: ndf_w3
    integer(kind=i_def)              :: undf_w3
    integer(kind=i_def)              :: max_stencil_size
    integer(kind=i_def)              :: cross_stencil_sizes(4)
    integer(kind=i_def), allocatable :: map_w3(:,:)
    integer(kind=i_def), allocatable :: cross1d_stencil_map(:,:,:)
    integer(kind=i_def), allocatable :: cross2d_stencil_map(:,:,:)
    integer(kind=i_def), allocatable :: halo_mask_x(:)
    integer(kind=i_def), allocatable :: halo_mask_y(:)
    integer(kind=i_def), allocatable :: halo_mask(:)
    integer(kind=i_def), allocatable :: halo_mask_x_answer(:)
    integer(kind=i_def), allocatable :: halo_mask_y_answer(:)

    ! Set integer values to describe mesh (single layer, 3x3 columns)
    nlayers = 1
    undf_w3 = 9
    ndf_w3 = 1
    cell = 5
    max_stencil_size = 2

    ! Canned field has extent of 1
    cross_stencil_sizes(:) = 2

    allocate(halo_mask_x(undf_w3))
    allocate(halo_mask_y(undf_w3))
    allocate(halo_mask(undf_w3))

    allocate(halo_mask_x_answer(undf_w3))
    allocate(halo_mask_y_answer(undf_w3))

    allocate(cross2d_stencil_map(1,2,4))

    ! Make canned maps
    call get_w3_m3x3_dofmap(map_w3, nlayers)
    call get_m3x3_stencil_dofmap_cross(cross1d_stencil_map, map_w3)

    ! Need to rearrange the 1D cross stencil into a 2D stencil for cell 5
    cross2d_stencil_map(1,1,:) = 5
    cross2d_stencil_map(1,2,1) = cross1d_stencil_map(1,2,cell)
    cross2d_stencil_map(1,2,2) = cross1d_stencil_map(1,3,cell)
    cross2d_stencil_map(1,2,3) = cross1d_stencil_map(1,4,cell)
    cross2d_stencil_map(1,2,4) = cross1d_stencil_map(1,5,cell)

    ! Test 1 -------------------------------------------------------------------
    ! Cell is by y halo edge
    halo_mask_x(:) = 0
    halo_mask_y(:) = 0
    halo_mask(:) = 1
    halo_mask(2) = 0
    halo_mask_x_answer(:) = 0
    halo_mask_y_answer(:) = 0
    halo_mask_y_answer(5) = 1

    call halo_mask_xy_code( nlayers,              &
                            halo_mask_x,          &
                            halo_mask_y,          &
                            halo_mask,            &
                            cross_stencil_sizes,  &
                            max_stencil_size,     &
                            cross2d_stencil_map,  &
                            ndf_w3,               &
                            undf_w3,              &
                            map_w3(:,cell) )

    ! Check values!
    @assertEqual( halo_mask_x_answer, halo_mask_x )
    @assertEqual( halo_mask_y_answer, halo_mask_y )

    ! Test 2 -------------------------------------------------------------------
    ! Cell is by x halo edge
    halo_mask_x(:) = 0
    halo_mask_y(:) = 0
    halo_mask(:) = 1
    halo_mask(4) = 0
    halo_mask_x_answer(:) = 0
    halo_mask_x_answer(5) = 1
    halo_mask_y_answer(:) = 0

    call halo_mask_xy_code( nlayers,              &
                            halo_mask_x,          &
                            halo_mask_y,          &
                            halo_mask,            &
                            cross_stencil_sizes,  &
                            max_stencil_size,     &
                            cross2d_stencil_map,  &
                            ndf_w3,               &
                            undf_w3,              &
                            map_w3(:,cell) )

    ! Check values!
    @assertEqual( halo_mask_x_answer, halo_mask_x )
    @assertEqual( halo_mask_y_answer, halo_mask_y )

    deallocate(map_w3)
    deallocate(cross1d_stencil_map)
    deallocate(halo_mask_x)
    deallocate(halo_mask_y)
    deallocate(halo_mask)
    deallocate(halo_mask_x_answer)
    deallocate(halo_mask_y_answer)
    deallocate(cross2d_stencil_map)

  end subroutine test_all

end module halo_mask_xy_kernel_mod_test
